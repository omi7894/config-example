import static nu.studer.gradle.jooq.JooqEdition.*

plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.15'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	id 'nu.studer.jooq' version '6.0.1'
}

group = 'com.hae'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '11'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2021.0.8")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	//implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	//implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	// H2 Database
	runtimeOnly 'com.h2database:h2'

	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.amqp:spring-rabbit-test'

	// mapstruct
	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

	// swagger-ui(https://mvnrepository.com/artifact/io.springfox/springfox-boot-starter)
	implementation 'io.springfox:springfox-boot-starter:3.0.0'

	// jooq
	jooqGenerator('org.mariadb.jdbc:mariadb-java-client')
	jooqGenerator("jakarta.xml.bind:jakarta.xml.bind-api:3.0.1")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}

sourceSets {
	main.java.srcDir "build/generated/jooq"
}

jooq {
	version = '3.15.1'  // default (can be omitted)
	edition = OSS  // default (can be omitted)

	configurations {
		main {  // name of the jOOQ configuration
			generateSchemaSourceOnCompilation = true  // default (can be omitted)

			generationTool {
				logging = org.jooq.meta.jaxb.Logging.WARN
				jdbc {
					driver = 'org.mariadb.jdbc.Driver'
					url = 'jdbc:mariadb://10.5.222.197:3306/approval'
					user = 'root'
					password = 'example'
					properties {
						property {
							key = 'PAGE_SIZE'
							value = 2048
						}
					}
				}
				generator {
					name = 'org.jooq.codegen.DefaultGenerator'
					database {
						name = 'org.jooq.meta.mariadb.MariaDBDatabase'
						inputSchema = 'approval'
						includes = '.*'
						excludes = ''
					}
					generate {
						deprecated = false
						records = true
						immutablePojos = true
						fluentSetters = true
					}
					target {
						packageName = 'com.hae.authority.dsl'
						directory = 'build/generated/jooq'  // default (can be omitted)
						encoding = 'UTF-8'
					}
					strategy {
						//name = 'com.hae.approval.config.CustomGeneratorStrategy'
						name = 'org.jooq.codegen.DefaultGeneratorStrategy'
					}
				}
			}
		}
	}
}